//===== Hercules Script =======================================
//= Swordsman Skills Quests
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= Hercules GIT
//===== Description: =========================================
//= Quests for skills: Fatal Blow, Mobile HP Recovery,
//  Auto-Berserk
//===== Additional Comments: =================================
//= 1.0 Moved from main folder. [Streusel]
//============================================================

izlude_in,175,130,2	script	Knight De Thomas	98,4,4,{
	if ((BaseClass == Job_Swordman) && (sm_movingrecovery_x < 1)) {
		mes "[De Thomas]";
		mes "Oh, no! You must have been hurt! Are you ok?";
		mes "You must have fought hard to get such serious injuries..";
		mes "Being a swordsman must come with a lot of responsibility and sacrifice.";
		next;
		mes "[De Thomas]";
		if (Sex == 0) {
			mes "For these swordsmen and knights, there is a wonderful skill.";
		}
		else {
			mes "For these swordswomen and knights, there is a wonderful skill young lady.";
		}
		mes "I present to you - HP Recovery While Moving!";
		mes "Body moving is a splendid skill";
		mes "that allows you to regain strength(HP)";
		mes "while you are moving!";
		next;
		mes "[De Thomas]";
		mes "It is currently under development";
		mes "so it may not recover that much,";
		mes "but it will help a little.";
		mes "What do you think? Would you like to learn this skill?";
		next;
		switch (select("What a nice skill! I want to learn it!:No, thank you.")) {
		case 1:
			mes "[De Thomas]";
			mes "Very well. I will tell you what you need to learn this skill.";
			mes "First, your job level must be higher than ^00880035^000000.";
			mes "You will also need ^008800200 empty bottles^000000.";
			mes "Why? Because it is proof that you fought fiercely to have used that many potions.";
			next;
			mes "[De Thomas]";
			mes "Also, the armor you used in battle.";
			mes "This is also proof of an experienced fighter.";
			mes "For the armor... your armor is perfect!";
			mes "Bring your armor!";
			mes "Last but not least... bring me one ^008800Moth Wing^000000.";
			next;
			menu "Eh? You need that, too?",-;
			mes "[De Thomas]";
			mes "Not really.. I don't really NEED it.";
			mes "It's just that my niece has gotten a bug hunting as a holiday task during the summer vacation.";
			mes "Of course! It would be much easier for me to get it myself.";
			mes "but I must work here all the time so I don't exactly have the time to go out and get it.";
			next;
			mes "[De Thomas]";
			mes "Don't you think it is pitiful that I have to stay in once place everyday, not being able to go outside?";
			mes "Please, find me one...*sniffsniff*";
			mes "If you don't...";
			set sm_movingrecovery_x,1;
			mes "You won't get anything! Muahahaha.";
			close;
		case 2:
			mes "[De Thomas]";
			mes "...";
			close;
		}
	}
	else if ((BaseClass == Job_Swordman) && (sm_movingrecovery_x > 1)) {
		mes "[De Thomas]";
		mes "Oh, it's you?";
		mes "Long time no see!";
		mes "You seem healthier than before.";
		mes "Hahahaha!";
		mes "Take care! See you again!";
		close;
	}
	else if ((BaseClass == Job_Swordman) && (sm_movingrecovery_x == 1)) {
		mes "[De Thomas]";
		mes "Welcome back...";
		mes "are you ready to learn Body Movin'?";
		next;
		switch (select("Yes.:No, I'm not ready yet.")) {
		case 1:
			if ((JobLevel > 34 || (BaseJob == Job_Knight || BaseJob == Job_Crusader)) && (countitem(713) > 199) && (countitem(1058) > 0)) {
				mes "[De Thomas]";
				mes "Let's see.....";
				next;
				mes "[De Thomas]";
				mes "Ok! I shall now teach you...";
				mes "...The Body Movin' skill!";
				next;
				delitem 713,200; //Empty_Bottle
				delitem 1058,1; //Wing_Of_Moth
				skill "SM_MOVINGRECOVERY",1,0;
				set sm_movingrecovery_x,2;
				mes "[De Thomas]";
				mes "There you go!";
				mes "Try it yourself.";
				mes "But don't overdo it.";
				next;
				mes "[De Thomas]";
				mes "Oh yeah, I won't be needing your";
				mes "armor so you can keep it.";
				mes "Good luck now!";
				close;
			}
			else if (JobLevel < 35 && (BaseJob != Job_Knight && BaseJob != Job_Crusader)) {
				mes "[De Thomas]";
				mes "Wait a second, your Job level isn't above ^00880035^000000!";
				mes "Come back when it is.";
				close;
			}
			else if ((countitem(713) < 200) || (countitem(1058) < 1)) {
				mes "[De Thomas]";
				mes "You do not have all the items I asked for.";
				next;
				mes "[De Thomas]";
				mes "Remember I need, ^008800200 empty bottles^000000, your armor, and a ^008800Moth Wing^000000. Come back when you have it all.";
				close;
			}
			mes "[De Thomas]";
			mes "You do not have all the items I asked for.";
			next;
			mes "[De Thomas]";
			mes "Remember I need, ^008800200 empty bottles^000000, your armor, and a ^008800Moth Wing^000000. Come back when you have it all.";
			close;
		case 2:
			mes "[De Thomas]";
			mes "Is that so?";
			mes "Then come when you are prepared.";
			close;
		}
	}
	mes "[De Thomas]";
	mes "My name is De Thomas Carlos.";
	mes "Knight of Prontera's 3rd Calvary.";
	mes "I have a certain duty these days.";
	mes "Ehem! Need I say more.";
	close;
}